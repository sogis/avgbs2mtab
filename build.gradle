plugins {
    id "com.jfrog.artifactory" version "4.5.2"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.github.johnrengelman.shadow"

group 'ch.so.agi.avgbs2mtab'
version '0.9.6'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://jars.interlis.ch"
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.poi', name: 'poi', version: '3.17'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    compile group: 'ch.interlis', name: 'iox-api', version: '1.0.3' 
    compile group: 'ch.interlis', name: 'iox-ili', version: '1.20.1' 
}

// Initialize properties for artifactory upload.
// Preventing gradle errors if gradle.properties is missing.
ext {
    setPropertyDefaultValue('artifactoryUser', "user")
    setPropertyDefaultValue('artifactoryPassword', "password")
}

String repo = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'

artifactory {
    contextUrl = "http://sogeo.services:8081/artifactory"
    publish {
        repository {
            //repoKey = 'libs-snapshot-local'
            repoKey = repo
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            // from components.java // standard use case
            // Is there a more elegant way for publishing the fat jar?
            // It's confusing too: artifact is named w/o "-all" suffix.
            artifact file("build/libs/" + project.name + "-" + project.version + "-all.jar")
        }
    }
}

def setPropertyDefaultValue(propertyName, defaultValue) {
    if (!project.hasProperty(propertyName)) {
        ext[propertyName] = defaultValue
    }
}


